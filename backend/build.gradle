buildscript {
    ext {
        queryDslVersion = '7.1'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {

    // annotationProcessor: 컴파일할 때만 실행되는 어노테이션 기반 소스코드 생성기
    // implementation: 애플리케이션의 핵심 의존성(컴파일, 런타임, 배포 단계에서 사용)
    // compileOnly: 컴파일 시에만 필요, 런타임, 배포 시에는 필요 없음
    // runtimeOnly: 실행, 배포할 때만 필요한 의존성. 인터페이스는 이미 있고, 구현체만 런타임에 끼워 넣는 구조
    //testImplementation : 테스트 코드 전용 의존성
    // developmentOnly: 개발 환경에서만 사용. 운영 환경에는 포함 x

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

//   ------------------- mapStruct -------------------

    // https://mvnrepository.com/artifact/org.mapstruct/mapstruct
    implementation 'org.mapstruct:mapstruct:1.6.3'

    // https://mvnrepository.com/artifact/org.mapstruct/mapstruct-processor
    implementation 'org.mapstruct:mapstruct-processor:1.6.3'


    //  ------------------- QueryDSL -------------------

//    querydsl-core	핵심 API	모든 QueryDSL 기능의 기반이 되는 공통 로직이 담긴 모듈입니다. 컬렉션 기반 쿼리, 타입 안전 쿼리 DSL 등
//    querydsl-apt	코드 생성기	Q클래스(ex. QUser)를 생성하기 위한 APT(Annotation Processing Tool). 컴파일 시 엔티티를 분석해 소스코드 생성
//    querydsl-jpa	JPA용 확장	JPA 환경(Spring Data JPA 포함)에서 사용하는 모듈
//    querydsl-sql	SQL용 확장	MyBatis, JDBC 등 JPA가 아닌 순수 SQL 환경에서 QueryDSL을 사용할 때 필요한 모듈

    // querydsl-jpa에 querydsl-core 포함됨
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${queryDslVersion}"

    // annotationProcessor: 컴파일할 때만 실행되는 어노테이션 기반 코드 생성기
    // Q클래스 생성용 따라서 컴파일 단계에서만 필요
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${queryDslVersion}:jpa"

//    공통 어노테이션(Annotation)들을 정의하는 API 모듈
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'


//            컴파일 시점: 자바 컴파일러(Javac)가 소스 코드를 빌드할 때, annotationProcessor로 등록된 녀석들(여기선 QueryDSL APT)이 동작
//
//            스캔: QueryDSL 프로세서는 코드 중에서 @Entity가 붙은 클래스(Board)를 찾습니다.
//
//            코드 생성: 찾은 클래스 정보를 바탕으로 Q클래스(QBoard)를 생성
//
//            저장: 작성한 Gradle 설정(src/main/generated) 경로에 Q클래스 파일들을 저장


    // Swagger configuration
    // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'
    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0'
    runtimeOnly 'com.github.therapi:therapi-runtime-javadoc:0.15.0'

    javadoc {
        options {
            encoding = 'UTF-8'          // Java 소스 파일의 인코딩
            charSet = 'UTF-8'           // 생성된 HTML 파일의 문자 인코딩
            docEncoding = 'UTF-8'       // JavaDoc의 출력 인코딩
            locale = 'ko_KR'            // JavaDoc 생성 시 사용할 로케일 설정
        }
    }

}

// Querydsl 빌드 옵션 설정
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [generated]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
}


tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}
